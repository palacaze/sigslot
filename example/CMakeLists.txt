add_custom_target(examples COMMENT "Build all the examples.")

macro(pal_create_example target ut)
    add_executable(${target} EXCLUDE_FROM_ALL "${ut}")
    target_link_libraries(${target} Pal::Sigslot)
    add_dependencies(examples ${target})
    pal_set_properties(${target} PRIVATE)
endmacro()

file(GLOB_RECURSE UNIT_TESTS RELATIVE ${CMAKE_CURRENT_SOURCE_DIR} "*.cpp")
foreach(ex IN LISTS UNIT_TESTS)
    string(REPLACE ".cpp" "" target ${ex})
    string(REGEX REPLACE "/" "." target ${target})

    if (target MATCHES "boost")
        if (Boost_FOUND)
            pal_create_example(${target} "${ex}")
        endif()
    elseif (target MATCHES "qt")
        if (Qt5_FOUND)
            pal_create_example(${target} "${ex}")
            target_link_libraries(${target} Qt5::Core)
        endif()
    elseif (target MATCHES "thread")
        pal_create_example(${target} "${ex}")
        if (Threads_FOUND)
            target_link_libraries(${target} Threads::Threads)
        endif()
    else()
        pal_create_example(${target} "${ex}")
    endif()
endforeach()

